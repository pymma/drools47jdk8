package org.drools.solver.evaluation

import org.drools.solver.core.evaluation.scorecalculator.HardAndSoftConstraintScoreCalculator;

import org.drools.solver.examples.travelingtournament.domain.Match;
import org.drools.solver.examples.travelingtournament.domain.Day;
import org.drools.solver.examples.travelingtournament.domain.Team;
import org.drools.solver.examples.travelingtournament.domain.TravelingTournament;
import org.drools.solver.examples.travelingtournament.solver.domain.Hop;
import org.drools.solver.examples.common.solver.domain.WorkaroundMultiplePatternAccumulate;

global HardAndSoftConstraintScoreCalculator scoreCalculator;

//query "multipleMatchesPerTeamPerDay"
//    $team : Team();
//    Match( $id : id, homeTeam == $team, $day : day )
//        || Match( $id : id, awayTeam == $team, $day : day );
//    exists Match( id > $id, homeTeam == $team, day == $day )
//        || exists Match(id > $id, awayTeam == $team, day == $day );
//end
//
//query "fourConsecutiveHomeMatches"
//    Match($homeTeam : homeTeam, $day1 : day);
//    Match(homeTeam == $homeTeam, eval(day.getIndex() == == $day1.getIndex() + 1));
//    Match(homeTeam == $homeTeam, eval(day.getIndex() == == $day1.getIndex() + 2));
//    Match(homeTeam == $homeTeam, eval(day.getIndex() == == $day1.getIndex() + 3));
//end
//
//query "fourConsecutiveAwayMatches"
//    Match($awayTeam : awayTeam, $day1 : day);
//    Match(awayTeam == $awayTeam, $day2 : day -> ($day2.getIndex() == $day1.getIndex() + 1));
//    Match(awayTeam == $awayTeam, $day3 : day -> ($day3.getIndex() == $day1.getIndex() + 2));
//    Match(awayTeam == $awayTeam, $day4 : day -> ($day4.getIndex() == $day1.getIndex() + 3));
//end
//
//query "matchRepeater"
//    Match($homeTeam : homeTeam, $awayTeam : awayTeam, $day1 : day);
//    Match(homeTeam == $awayTeam, awayTeam == $homeTeam, $day2 : day -> ($day2.getIndex() == $day1.getIndex() + 1));
//end

rule "multipleMatchesPerTeamPerDay"
  when
    $team : Team();
    $m : Match( $id : id, homeTeam == $team, $day : day )
        || $m : Match( $id : id, awayTeam == $team, $day : day );
    exists Match( id > $id, homeTeam == $team, day == $day )
        || exists Match(id > $id, awayTeam == $team, day == $day );
  then
    insertLogical(new WorkaroundMultiplePatternAccumulate("multipleMatchesPerTeamPerDay", $m));
end

rule "fourConsecutiveHomeMatches"
  when
    $m : Match($homeTeam : homeTeam, $day1 : day);
    Match(homeTeam == $homeTeam, eval(day.getIndex() == $day1.getIndex() + 1));
    Match(homeTeam == $homeTeam, eval(day.getIndex() == $day1.getIndex() + 2));
    Match(homeTeam == $homeTeam, eval(day.getIndex() == $day1.getIndex() + 3));
  then
    insertLogical(new WorkaroundMultiplePatternAccumulate("fourConsecutiveHomeMatches", $m));
end

rule "fourConsecutiveAwayMatches"
  when
    $m : Match($awayTeam : awayTeam, $day1 : day);
    Match(awayTeam == $awayTeam, eval(day.getIndex() == $day1.getIndex() + 1));
    Match(awayTeam == $awayTeam, eval(day.getIndex() == $day1.getIndex() + 2));
    Match(awayTeam == $awayTeam, eval(day.getIndex() == $day1.getIndex() + 3));
  then
    insertLogical(new WorkaroundMultiplePatternAccumulate("fourConsecutiveAwayMatches", $m));
end

rule "matchRepeater"
  when
    $m : Match($homeTeam : homeTeam, $awayTeam : awayTeam, $day1 : day);
    Match(homeTeam == $awayTeam, awayTeam == $homeTeam, eval(day.getIndex() == $day1.getIndex() + 1));
  then
    insertLogical(new WorkaroundMultiplePatternAccumulate("matchRepeater", $m));
end

rule "hardConstraintsBroken"
  salience -1 // Finish the other rules first (optional, for performance)
  when
    $hardCount : Long() from accumulate(
       $w : WorkaroundMultiplePatternAccumulate(),
       count($w)
    );
  then
    scoreCalculator.setHardConstraintsBroken($hardCount.intValue());
end


rule "startToAwayHop"
  when
    Match($toTeam : homeTeam, $team : awayTeam, $day : day);
    not Day(eval(index == $day.getIndex() - 1));
  then
    insertLogical(new Hop($team, $team, $toTeam));
end
rule "homeToAwayHop"
  when
    Match($team : homeTeam, $day1 : day);
    Match($toTeam : homeTeam, awayTeam == $team, eval(day.getIndex() == $day1.getIndex() + 1));
  then
    insertLogical(new Hop($team, $team, $toTeam));
end
rule "awayToAwayHop"
  when
    Match($fromTeam : homeTeam, $team : awayTeam, $day1 : day);
    Match($toTeam : homeTeam, awayTeam == $team, eval(day.getIndex() == $day1.getIndex() + 1));
  then
    insertLogical(new Hop($team, $fromTeam, $toTeam));
end
rule "awayToHomeHop"
  when
    Match($fromTeam : homeTeam, $team : awayTeam, $day1 : day);
    Match(homeTeam == $team, eval(day.getIndex() == $day1.getIndex() + 1));
  then
    insertLogical(new Hop($team, $fromTeam, $team));
end
rule "awayToEndHop"
  when
    Match($fromTeam : homeTeam, $team : awayTeam, $day : day);
    not Day(eval(index == $day.getIndex() + 1));
  then
    insertLogical(new Hop($team, $fromTeam, $team));
end

rule "softConstraintsBroken"
  salience -1 // Finish the other rules first (optional, for performance)
  when
    $totalDistance : Double() from accumulate(
       Hop($distance : distance),
       sum($distance)
    );
  then
    scoreCalculator.setSoftConstraintsBroken($totalDistance.intValue());
end